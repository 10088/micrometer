buildscript {
    repositories {
      jcenter()
      maven { url 'https://plugins.gradle.org/m2/' }
    }
    dependencies {
        classpath 'io.spring.gradle:spring-release-plugin:0.2.0'
        classpath 'com.netflix.nebula:gradle-extra-configurations-plugin:3.2.0'
        classpath 'me.champeau.gradle:jmh-gradle-plugin:0.4.4'
        classpath 'org.junit.platform:junit-platform-gradle-plugin:1.0.0-M4'
        classpath 'com.netflix.nebula:nebula-project-plugin:3.4.0'
    }
}

allprojects {
    apply plugin: 'nebula.release'
}

subprojects {
    apply plugin: 'io.spring.release'
    apply plugin: 'java'

    group = 'io.micrometer'
    description = 'Application monitoring instrumentation facade'

    contacts {
        'jschneider@pivotal.io' {
            moniker 'Jon Schneider'
            github 'jkschneider'
        }
    }

    repositories {
        mavenCentral()

        // TODO just temporarily, for reactor snapshots
        maven { url 'https://repo.spring.io/libs-snapshot' }
    }

    dependencies {
        // JUnit 5
        testCompile 'org.junit.platform:junit-platform-launcher:1.0.0-M5' // see https://github.com/junit-team/junit5/issues/586 for why this is necessary
        testCompile 'org.junit.jupiter:junit-jupiter-api:5.0.0-M5'
        testCompile 'org.junit.jupiter:junit-jupiter-params:5.0.0-M5'
        testRuntime 'org.junit.jupiter:junit-jupiter-engine:5.0.0-M5'

        testCompile 'org.assertj:assertj-core:3.+'
    }

    if(project.extensions.findByName('bintray')) {
        bintray.pkg {
            labels = ['micrometer', 'atlas', 'metrics', 'prometheus', 'spectator', 'influx']
        }
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = '4.0.1'
}
