plugins {
    id 'maven-publish'
    id 'io.spring.dependency-management' version '1.0.8.RELEASE'
}

description 'Micrometer BOM (Bill of Materials) for managing Micrometer artifact versions'

dependencyManagement {
    dependencies {
        rootProject.subprojects.findAll {
            !it.name.contains('sample') &&
                    !it.name.contains('benchmark') &&
                    !it.name.contains('micrometer-bom')
        }.each {
            dependency(group: it.group,
                    name: it.name,
                    version: it.version.toString())
        }
    }
}

bintray.publication = 'mavenBom'
artifactory {
    publish {
        defaults {
            publications 'mavenBom'
        }
    }
}

publishing {
    publications {
        mavenBom(MavenPublication) {
        }
    }
}

tasks.withType(PublishToMavenLocal) {
    onlyIf {
        // avoid publishing `nebula` publication added by plugins
        publication == publishing.publications.mavenBom
    }
}
