pluginManagement {
    repositories {
        mavenCentral()
        maven { url 'https://repo.spring.io/release' }
        gradlePluginPortal {
            content {
                includeGroup('org.gradle')
                includeGroup('org.gradle.kotlin')
                includeGroup('org.gradle.kotlin.kotlin-dsl')

                includeGroup('com.gradle')
                includeGroup('com.gradle.enterprise')

                includeGroup('me.champeau.jmh')
                includeGroup('io.github.gradle-nexus')
                includeGroup('gradle.plugin.com.hierynomus.gradle.plugins')
                includeGroup('org.ajoberstar.grgit')
                includeGroup('io.spring.nohttp')
                includeGroup('com.github.johnrengelman.shadow')
                includeGroup('gradle.plugin.com.github.johnrengelman')
            }

            metadataSources {
                mavenPom()
                ignoreGradleMetadataRedirection()
            }
        }
    }
}

plugins {
    id 'com.gradle.enterprise' version '3.8'
    id 'io.spring.ge.conventions' version '0.0.9'
}

rootProject.name = 'micrometer'

buildCache {
    remote(HttpBuildCache) {
        url = 'https://ge.micrometer.io/cache/'
    }
}
gradleEnterprise {
    server = 'https://ge.micrometer.io'
}

include 'micrometer-core'

['core', 'boot2', 'boot2-reactive', 'spring-integration', 'hazelcast', 'hazelcast3', 'javalin', 'jersey3'].each { sample ->
    include "micrometer-samples-$sample"
    project(":micrometer-samples-$sample").projectDir = new File(rootProject.projectDir, "samples/micrometer-samples-$sample")
}

include 'micrometer-test'

['atlas', 'prometheus', 'datadog', 'elastic', 'ganglia', 'graphite', 'health', 'jmx', 'influx', 'statsd', 'new-relic', 'cloudwatch', 'cloudwatch2', 'signalfx', 'wavefront', 'dynatrace', 'azure-monitor', 'humio', 'appoptics', 'kairos', 'stackdriver', 'opentsdb'].each { sys ->
    include "micrometer-registry-$sys"
    project(":micrometer-registry-$sys").projectDir = new File(rootProject.projectDir, "implementations/micrometer-registry-$sys")
}

['core'].each { benchmark ->
    include "micrometer-benchmarks-$benchmark"
    project(":micrometer-benchmarks-$benchmark").projectDir = new File(rootProject.projectDir, "benchmarks/benchmarks-$benchmark")
}

include 'micrometer-bom'
